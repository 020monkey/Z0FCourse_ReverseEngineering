# 8.04 Get Element - WIP - NOT READY
# TODO: SET CONSISTENT ADDRESSES
Now let's look at a function that has to do with looking data up in the table. Remember, it doesn't matter which functions we reverse first. I'm choosing based on what I think will be a good order to go in.

There are multiple functions that might get something from the table: `RtlEnumerateGenericTable`, `RtlGetElementGenericTable`, `RtlLookupElementGenericTable`, and some others. Based on the names, I think `RtlGetElementGenericTable` or `RtlLookupElementGenericTable` will be the easiest. Out of those two I'd guess `RtlGetElementGenericTable` will be the simplest. I'd guess that it simply looks in the table at the index passed to the function. This is just a guess, I don't even know what parameters it takes yet.

Disassembly of `RtlGetElementGenericTable`:
```asm
7FF8BBD1C1B0    MOV QWORD PTR SS:[RSP+0x8], RBX
7FF8BBD1C1B5    MOV R10D, DWORD PTR DS:[RCX+0x20]
7FF8BBD1C1B9    LEA R11D, QWORD PTR DS:[RDX+0x1]
7FF8BBD1C1BD    MOV R8, QWORD PTR DS:[RCX+0x18]
7FF8BBD1C1C1    OR EBX, 0xFFFFFFFF
7FF8BBD1C1C4    MOV R9D, R11D
7FF8BBD1C1C7    CMP EDX, EBX
7FF8BBD1C1C9    JE ntdll.7FF8BBD1C21C
7FF8BBD1C1CB    MOV EAX, DWORD PTR DS:[RCX+0x24]
7FF8BBD1C1CE    CMP R11D, EAX
7FF8BBD1C1D1    JA ntdll.7FF8BBD1C21C
7FF8BBD1C1D3    CMP R11D, R10D
7FF8BBD1C1D6    JE ntdll.7FF8BBD1C200
7FF8BBD1C1D8    JB ntdll.7FF8BBD66C7A
7FF8BBD1C1DE    SUB EAX, R11D
7FF8BBD1C1E1    MOV EDX, R11D
7FF8BBD1C1E4    SUB EDX, R10D
7FF8BBD1C1E7    INC EAX
7FF8BBD1C1E9    CMP EDX, EAX
7FF8BBD1C1EB    JA ntdll.7FF8BBD1C20A
7FF8BBD1C1ED    TEST EDX, EDX
7FF8BBD1C1EF    JE ntdll.7FF8BBD1C1F8
7FF8BBD1C1F1    MOV R8, QWORD PTR DS:[R8]
7FF8BBD1C1F4    ADD EDX, EBX
7FF8BBD1C1F6    JNE ntdll.7FF8BBD1C1F1
7FF8BBD1C1F8    MOV QWORD PTR DS:[RCX+0x18], R8
7FF8BBD1C1FC    MOV DWORD PTR DS:[RCX+0x20], R11D
7FF8BBD1C200    LEA RAX, QWORD PTR DS:[R8+0x10]
7FF8BBD1C204    MOV RBX, QWORD PTR SS:[RSP+0x8]
7FF8BBD1C209    RET
7FF8BBD1C20A    LEA R8, QWORD PTR DS:[RCX+0x8]
7FF8BBD1C20E    TEST EAX, EAX
7FF8BBD1C210    JE ntdll.7FF8BBD1C1F8
7FF8BBD1C212    MOV R8, QWORD PTR DS:[R8+0x8]
7FF8BBD1C216    ADD EAX, EBX
7FF8BBD1C218    JE ntdll.7FF8BBD1C1F8
7FF8BBD1C21A    JMP ntdll.7FF8BBD1C212
7FF8BBD1C21C    XOR EAX, EAX
7FF8BBD1C21E    JMP ntdll.7FF8BBD1C204
```

Before we get into this function, I'm going to offer you a hint, otherwise you may get very confused. If you don't want the hint, move on. If you're not sure if you want the hint, I'll let you know that is has to do with a computer science topic. More specifically, data structures. Now for the spoiler. This function is going to deal with a splay tree. For our purposes we can view it as a linked list. We can figure this out by reversing, but if you don't know these topics you will be very confused. If you need to, go learn these topics before continuing.

### General Overview
When a function is pretty involved, I always like going through it and identifying the obvious stuff, then going back through and analyzing more closely. This will help me get a general idea of what's going on. The first thing we'll do is check for function parameters.
* RCX is used like a data structure (`MOV EAX, DWORD PTR DS:[RCX+0x24]`), so it's probably the table like usual.
* RDX is also used, but with an offset of +0x1 (`LEA R11D, QWORD PTR DS:[RDX+0x1]`). RDX could be a data structure, maybe a pointer, or maybe it's just being incremented. We'll figure that out later, just note that RDX is used.
* R8 and R9 are overwritten, so they're probably not parameters.
We can guess that this function only takes 2 parameters. The first one being the table, the second one is only a guess but, based on the function name, it could be the index to find.

There are two jumps that are interesting, which go to `ntdll.7FF8BBD1C21C`. `ntdll.7FF8BBD1C21C` will set the return value to zero, then jump to `ntdll.7FF8BBD1C204` where RBX is restored. Finally, the function returns. This is likely a fail condition.

>If you don't already, I'd highly recommend writing notes and/or pseudo code as you are reverse engineering.

# Part 1
Let's focus on the following portion of code:
```asm
MOV QWORD PTR SS:[RSP+0x8], RBX
MOV R10D, DWORD PTR DS:[RCX+0x20]
LEA R11D, QWORD PTR DS:[RDX+0x1]
MOV R8, QWORD PTR DS:[RCX+0x18]
OR EBX, 0xFFFFFFFF
MOV R9D, R11D
CMP EDX, EBX
JE ntdll.7FF8BBD1C21C
MOV EAX, DWORD PTR DS:[RCX+0x24]
CMP R11D, EAX
JA ntdll.7FF8BBD1C21C
```
* RBX is preserved/saved on the stack.
* `Table->Member5` is moved into R10D.
* `LEA R11D, QWORD PTR DS:[RDX+0x1]` is an interesting instruction. It's essentially `R11D = RDX + 1`. This might be done instead of using `INC` to preserve the value of RDX. Let's call R11D the AdjustedIndex.
* Next, the fourth member of the table is moved into R8.
* `OR EBX, 0xFFFFFFFF` sets EBX to -1.
* R11D (AdjustedIndex) is moved into R9D. Again, this is a little weird at first, why not just use R11D? This is probably done to preserve R11D.
* EDX (second parameter) is then compared to EBX (-1). This tells us a few things. First, we can be even more sure that EDX is an index. Also, this further suggests that the NumberOfElements member is 4 bytes unlike the others, and is at offset +24.
  * If the index is -1, then jump to `ntdll.7FF8BBD1C21C` which is the fail condition.
* `MOV EAX, DWORD PTR DS:[RCX+0x24]` moves the NumberOfElements member into EAX.
* R11D (AdjustedIndex) is then checked if it's greater than EAX (NumberOfElements). This is just some more index validation. This also tells us another piece of useful information, the index is zero-based. This means it starts from index zero, not 1, similar to a conventional array. This makes sense because the number of elements is *not* zero-based (0 elements would mean it's empty). Again, this is just like an array.
  * If R11D is greater than EAX, then it jumps to the fail condition. `JA` tells us that it's unsigned.

Here is some pseudo code to ease your mind:
```c
ULONG adjustedIndex = index + 1;
if(index == -1 || adjustedIndex > Table->NumberOfElements){
    return 0;
}
```

# Part 2
Picking up where we were:
```asm
7FF8BBD1C1D3    CMP R11D, R10D          ; R11D = AdjustedIndex, R10D = Table->Member5
7FF8BBD1C1D6    JE ntdll.7FF8BBD1C200
7FF8BBD1C1D8    JB ntdll.7FF8BBD66C7A
7FF8BBD1C1DE    SUB EAX, R11D           ; EAX = Table->NumberOfElements
7FF8BBD1C1E1    MOV EDX, R11D
7FF8BBD1C1E4    SUB EDX, R10D
7FF8BBD1C1E7    INC EAX
7FF8BBD1C1E9    CMP EDX, EAX
7FF8BBD1C1EB    JA ntdll.7FF8BBD1C20A
```
Here is where things start to get interesting.
* `CMP R11D, R10D` compares AdjustedIndex to the fifth member in the table. This tells us that the fifth member in the table likely has to do with an index. Maybe it stores the last index, first index, the previously found index, or even some kind of special index. Whatever it is, we'll find out. Just keep this all in mind.
  * If they are equal, jump to `ntdll.7FF8BBD1C200`.
  * If Index+1 is less than (JB is Jump if Below) the fifth member, jump to `ntdll.7FF8BBD66C7A`.
    * We'll worry about where these jumps go later. We'll proceed as if these jumps weren't taken, which means Index+1 is greater than Table->Member5.
* `SUB EAX, R11D` - EAX contains Table->NumberOfElements. This instruction is effectively `EAX = NumberOfElements - AdjustedIndex`.
* `MOV EDX, R11D` copies R11D into EDX so that value can be sued in the following instructions while also being preserved.
* `SUB EDX, R10D` is the same as `EDX = AdjustedIndex - Member5`.
* `INC EAX` is the same as `EAX = (NumberOfElements - AdjustedIndex) + 1`.
* `CMP EDX, EAX` compares EDX (AdjustedIndex - Member5) to EAX (NumberOfElements - AdjustedIndex).
* There are then a series of conditions. Before we go any further, let's try to make more sense of what just happened.

To help understand what's going on, let's use some of our own values and see what happens. Let's say that NumberOfElements is 10, and the index is 2. We don't know what Table->Member5 is yet, but you can start to make some good guesses.

```asm
7FF8BBD1C1D3    CMP 3, Table->Member5
7FF8BBD1C1D6    JE ntdll.7FF8BBD1C200
7FF8BBD1C1D8    JB ntdll.7FF8BBD66C7A
7FF8BBD1C1DE    EAX = Table->NumberOfElements - 3
7FF8BBD1C1E4    EDX = 3 - Table->Member5
7FF8BBD1C1E7    EAX++
7FF8BBD1C1E9    CMP EDX, EAX    ; CMP (3 - Table->Member5) to (Table->NumberOfElements - 3).
```

Okay, this is where we can really start to predict what Member5 is. It's clearly an index of some kind. Let's take a look at some possible scenarios.

If Member5 is the maximum index:
```asm
CMP (3 - 10) to (10 - 3)
```
If Member5 is the minimum index:
```asm
CMP (3 - 1) to (1 - 3)
```
If Member5 is the last found index (7 for example):
```asm
CMP (3 - 7) to (7 - 3)
```
Member5 being the minimum index wouldn't serve any good purpose. The maximum index wouldn't be very helpful either. But if it's the last index found, it could be quite helpful. For example, if the index and the last index found are the same then you can just return the last index found. If the index is greater than the last index found, you know to look above the last index found. If the index is less than the last index found, search below the last index found. This would help improve performance. We still can't be certain, but let's continue with the assumption that Member5 is the last index found.