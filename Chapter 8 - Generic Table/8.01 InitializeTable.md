# 8.01 Initialize Table - **Needs Proof-reading**
Based on the function names we can guess that the "Generic Table" is probably some kind of data structure. Because of this, it's probably a good idea to start by looking at the initialization function(s). This will hint at what the table contains, or at least how many things it contains.

#### Disassembly of `RtlInitializeGenericTable`:
```asm
LEA RAX, QWORD PTR DS:[RCX + 0x8]
XOR R10D, R10D
MOV QWORD PTR DS:[RCX], R10
MOV QWORD PTR DS:[RAX + 0x8], RAX
MOV QWORD PTR DS:[RAX], RAX
MOV QWORD PTR DS:[RCX + 0x18], RAX
MOV RAX, QWORD PTR SS:[RSP + 0x28]
MOV QWORD PTR DS:[RCX + 0x40], RAX
MOV QWORD PTR DS:[RCX + 0x20], R10
MOV QWORD PTR DS:[RCX + 0x28], RDX
MOV QWORD PTR DS:[RCX + 0x30], R8
MOV QWORD PTR DS:[RCX + 0x38], R9
RET 
```

Notice that this function doesn't subtract from RSP like normal. That's because the function doesn't have any local variables.

RCX, RDX, R8, and R9 are all being used. This means this function takes at least 4 parameters.
Right away we can see that RCX is some kind of data structure, probably a generic structure, because of how it's being used. Remember, data structures are referenced from a base address (RCX in this case) plus an offset (such as +0x40). This tells us that the first parameter of this function is a data structure, probably a generic table, which we'll just call a table for simplicity. RCX+0x28 is also used, which is the fifth parameter in this case.

>"DS" is short for Data Segment, you can ignore it.

## Parameter Types
Now we know the function takes 5 parameters, what are their types? The first parameter, RCX, is a data structure. We can guess that it's a generic table based on the function name. You could look up what a generic table is, but I would recommend not doing that in this case so we can learn by doing as much as possible. However, when you're actually reversing something feel free to look up whatever you want. 

Next up, RDX, R8, and R9. They are all very similar. They all use `MOV QWORD PTR DS:[RCX + OFFSET], RDX/R8/R9`. This tells us that the parameters are 8 byte (QWORD) pointers (which makes sense because x64 addresses are 8 bytes). 

## Data
Let's analyze what's being moved into the data structure. Based on our previous observations, we can safely assume that the data structure in RCX is a class or struct. However, keep an open mind just in case we're wrong.
* The first line passes the address of the second member in the table (RCX+0x8) into RAX.
* R10 is zeroed out.
* R10 (0) is moved into the first member (RCX). RCX is the first member in the table (also the address used to reference the table).
* RAX (the second member) is then moved into the element at RAX+0x8, which is the 3rd member because RAX already contains RCX+0x8. This is effectively RCX+0x8+0x8 or RCX+0x10. 
* The address that RAX holds (the second member) is then moved into the second member of the data structure (RAX, which holds RCX+0x8).
* RAX is then moved into the element at RCX+0x18, which is the fourth element.
Before continuing further, that was a little all over the place. To help us understand, let's correlate the assembly with C code.

```asm
LEA RAX, QWORD PTR DS:[RCX + 0x8]
XOR R10D, R10D
MOV QWORD PTR DS:[RCX], R10         ;Struct->Member1 = 0;
MOV QWORD PTR DS:[RAX + 0x8], RAX   ;Struct->Memeber3 = &Struct->Member2;
MOV QWORD PTR DS:[RAX], RAX         ;Struct->Memeber2 = &Struct->Member2;
MOV QWORD PTR DS:[RCX + 0x18], RAX  ;Struct->Memeber4 = &Struct->Member2;
```

Alright, that wasn't too bad. Let's keep going. We're picking up at `MOV RAX, QWORD PTR SS:[RSP + 0x28]`.
* The fifth function parameter (RSP + 0x28) is passed into RAX.
* RAX is moved into the 9th element.
* R10 (0) is moved into the 5th element.
* RDX is moved into the 6th element.
* R8 is moved into the 7th element.
* R9 is moved into the 8th element.

Comparing assembly to C again:
```asm
MOV RAX, QWORD PTR SS:[RSP + 0x28]
MOV QWORD PTR DS:[RCX + 0x40], RAX  ;Struct->Member9 = Param5
MOV QWORD PTR DS:[RCX + 0x20], R10  ;Struct->Member5 = 0
MOV QWORD PTR DS:[RCX + 0x28], RDX  ;Struct->Member6 = Param2
MOV QWORD PTR DS:[RCX + 0x30], R8   ;Struct->Member7 = Param3
MOV QWORD PTR DS:[RCX + 0x38], R9   ;Struct->Member8 = Param4
RET 
```

## Bringing the Information Together
The table has 9 elements (that we know of). We know that the 2nd, 3rd, and 4th elements are all pointers to the second element. Members 1 and 5 are initialized to zero. Based on the fact that each element is 8 bytes and there are 9 elements, we can conclude that the table has a size of 72 bytes. We can also determine that because the final offset is 0x40 so there is a total of 0x48 bytes of data, and therefore is 72 bytes.

>0x48 bytes of data and not 0x40 bytes because the final element, at the offset of 0x40, has a size of 8 bytes.

With this information, we now understand the table a bit more. We can also construct a foundational layout of the table:

```c
struct Table{
    QWORD Member1;
    QWORD_PTR Member2;
    QWORD_PTR Member3;
    QWORD_PTR Member4;
    QWORD Member5;
    QWORD Member6;
    QWORD Member7;
    QWORD Member8;
    QWORD Member9;    
};
```

Quick note: I'm declaring the data as "QWORD" because that's how big it's said to be according to the assembly code. I'm *not* doing it because that's what the data actually represents in the source code. As of now, we have no idea what the data represents. Similarly, if I'm not sure if an element is a char or bool, but I know it's a byte, I'll declare it as BYTE. Some reverse engineers would prefer to write it as:

```c
struct Table{
    UNKNOWN Member1;
    UNKNOWN_PTR Member2;
    UNKNOWN_PTR Member3;
    UNKNOWN_PTR Member4;
    UNKNOWN Member5;
    UNKNOWN Member6;
    UNKNOWN Member7;
    UNKNOWN Member8;
    UNKNOWN Member9;    
};
```

I prefer the QWORD option just so I always know at a glance how big an element is, so that's how I'll continue to write it throughout this course. Again, I'm just specifying it's size until I find out what it actually is.

That's it for this function, hopefully, it was pretty fun.

[<- Previous Lesson](8.00%20GenericTable.md)  
[Next Lesson ->]() - WIP  

[Chapter Home](8.00%20GenericTable.md)  